/********************************************************************
  ECEN 240/301 Lab Code
  Light-Following Robot

  The approach of this code is to use an architectured that employs
  three different processes:
    Perception
    Planning
    Action

  By separating these processes, this allows one to focus on the
  individual elements needed to do these tasks that are general
  to most robotics.

 ********************************************************************/

/* These initial includes allow you to use necessary libraries for
your sensors and servos. */
#include "Arduino.h"
#include <CapacitiveSensor.h>

//
// Compiler defines: the compiler replaces each name with its assignment
// (These make your code so much more readable.)
//

/***********************************************************/
// Hardware pin definitions
// Replace the pin numbers with those you connect to your robot

// Button pins. These will be replaced with the photodiode variables in lab 5
//#define BUTTON_1  A2     // Far left Button - Servo Up
//#define BUTTON_2  A3     // Left middle button - Left Motor
#define BUTTON_3  A4     // Middle Button - Collision
//#define BUTTON_4  A5     // Right middle button - Right Motor
//#define BUTTON_5  A6     // Far right button - Servo Down
//#define BUTTON_TEMP  A0

// LED pins (note that digital pins do not need "D" in front of them)
#define LED_1   6       // Far Left LED - Servo Up
#define LED_3   4       // Middle LED - Collision
#define LED_5   2       // Far Right LED - Servo Down


// Motor enable pins - Lab 3
#define H_BRIDGE_ENA  5 //LED_2
#define H_BRIDGE_ENB  3  //LED_4

// Photodiode pins - Lab 5
// These will replace buttons 1, 2, 4, 5
#define PHOTODIODE_1 A7
#define PHOTODIODE_2 A6
#define PHOTODIODE_3 A5
#define PHOTODIODE_4 A3

// Capacitive sensor pins - Lab 4
#define CAP_SENSOR_SEND     8
#define CAP_SENSOR_RECEIVE  7
#define CAP_SENSOR_SAMPLES 40
#define CAP_SENSOR_TAU_THRESHOLD 100


// Ultrasonic sensor pin - Lab 6
// This will replace button 3 and LED 3 will no longer be needed

// Servo pin - Lab 6
// This will replace LEDs 1 and 5

/***********************************************************/
// Configuration parameter definitions
// Replace the parameters with those that are appropriate for your robot

// Voltage at which a button is considered to be pressed
#define BUTTON_THRESHOLD 2.5

// Voltage at which a photodiode voltage is considered to be present - Lab 5
#define PHOTODIODE_LIGHT_THRESHOLD 3


// Number of samples that the capacitor sensor will use in a measurement - Lab 4


// Parameters for servo control as well as instantiation - Lab 6


// Parameters for ultrasonic sensor and instantiation - Lab 6


// Parameter to define when the ultrasonic sensor detects a collision - Lab 6



/***********************************************************/
// Defintions that allow one to set states
// Sensor state definitions
#define DETECTION_NO    0
#define DETECTION_YES   1

// Motor speed definitions - Lab 4
#define SPEED_0 0;
#define SPEED_1 (int)(255 * .33);
#define SPEED_2 (int)(255 * .66); 
#define SPEED_3 (int)(255);

// Collision definitions
#define COLLISION_ON   0
#define COLLISION_OFF  1

// Driving direction definitions
#define DRIVE_STOP      0
#define DRIVE_LEFT      1
#define DRIVE_RIGHT     2
#define DRIVE_STRAIGHT  3

// Servo movement definitions
#define SERVO_MOVE_STOP 0
#define SERVO_MOVE_UP   1
#define SERVO_MOVE_DOWN 2


/***********************************************************/
// Global variables that define PERCEPTION and initialization

// Collision (using Definitions)
int SensedCollision = DETECTION_NO;

// Photodiode inputs (using Definitions) - The button represent the photodiodes for lab 2
int SensedLightRight = DETECTION_NO;
int SensedLightLeft = DETECTION_NO;
int SensedLightUp = DETECTION_NO;
int SensedLightDown = DETECTION_NO;

// Capacitive sensor input (using Definitions) - Lab 4
int SensedCapacitiveTouch = DETECTION_NO;


/***********************************************************/
// Global variables that define ACTION and initialization

// Collision Actions (using Definitions)
int ActionCollision = COLLISION_OFF;

// Main motors Action (using Definitions)
int ActionRobotDrive = DRIVE_STOP;

// Add speed action
int ActionRobotSpeed = SPEED_3;

// Servo Action (using Definitions)
int ActionServoMove =  SERVO_MOVE_STOP;

/********************************************************************
  SETUP function - this gets executed at power up, or after a reset
 ********************************************************************/
void setup() {
  //Set up serial connection at 9600 Baud
  Serial.begin(9600);
  
  //Set up output pins
  pinMode(LED_1, OUTPUT);
  pinMode(H_BRIDGE_ENA, OUTPUT);
  pinMode(LED_3, OUTPUT);
  pinMode(H_BRIDGE_ENB, OUTPUT);
  pinMode(LED_5, OUTPUT);
  
  //Set up input pins
  pinMode(PHOTODIODE_1, INPUT);
  pinMode(PHOTODIODE_2, INPUT);
  pinMode(BUTTON_3, INPUT);
  pinMode(PHOTODIODE_3, INPUT);
  pinMode(PHOTODIODE_4, INPUT);

  //Set up capacitance pins
  pinMode(CAP_SENSOR_RECEIVE, INPUT);
  pinMode(CAP_SENSOR_SEND, OUTPUT); 

  //Set up servo - Lab 6

}

/********************************************************************
  Main LOOP function - this gets executed in an infinite loop until
  power off or reset. - Notice: PERCEPTION, PLANNING, ACTION
 ********************************************************************/
void loop() {
  // This DebugStateOutput flag can be used to easily turn on the
  // serial debugging to know what the robot is perceiving and what
  // actions the robot wants to take.
  int DebugStateOutput = false; // Change false to true to debug
  
  
  RobotPerception(); // PERCEPTION
  if (DebugStateOutput) {
    Serial.print("Perception:");
    Serial.print(SensedLightUp);
    Serial.print(SensedLightLeft);
    Serial.print(SensedCollision);
    Serial.print(SensedLightRight); 
    Serial.print(SensedLightDown);
    Serial.print(SensedCapacitiveTouch);
    Serial.print("\t");
  }
  
  RobotPlanning(); // PLANNING
  if (DebugStateOutput) {
    Serial.print(" Action:");
    Serial.print(ActionCollision);
    Serial.print(ActionRobotDrive); 
    Serial.print(ActionServoMove);
    Serial.print(" "); Serial.print(ActionRobotSpeed);
    Serial.print("\t");
  }
  RobotAction(); // ACTION
  Serial.print("\n");

  //isCapacitiveSensorTouched();
  //isLight(PHOTODIODE_1);
}

/**********************************************************************************************************
  Robot PERCEPTION - all of the sensing
 ********************************************************************/
void RobotPerception() {
  // This function polls all of the sensors and then assigns sensor outputs
  // that can be used by the robot in subsequent stages


  // Photodiode Sensing
  //Serial.print(getPinVoltage(BUTTON_2)); Serial.print("\t"); //uncomment for debugging
  
  if (isLight(PHOTODIODE_1)){
    SensedLightLeft = DETECTION_YES;
  } else {
    SensedLightLeft = DETECTION_NO;
  }
  // Remember, you can find the buttons and which one goes to what towards the top of the file
  if (isLight(PHOTODIODE_2)){
    SensedLightRight= DETECTION_YES;
  } else {
    SensedLightRight = DETECTION_NO;
  }

      
  // logic for sensing button 1 presses
  if (isLight(PHOTODIODE_3)){
    SensedLightDown = DETECTION_YES;
  } else {
    SensedLightDown = DETECTION_NO;
  }
  // logic for sensing button 5 pressed
  if (isLight(PHOTODIODE_4)){
    SensedLightUp = DETECTION_YES;
  } else {
    SensedLightUp = DETECTION_NO;
  }
  

   // Capacitive Sensor
   if (isCapacitiveSensorTouched()){
    SensedCapacitiveTouch = DETECTION_YES;
   } else {
    SensedCapacitiveTouch = DETECTION_NO;
   }

   // Collision Sensor
   if (isCollision()) {
    SensedCollision = DETECTION_YES;
   } else {
    SensedCollision = DETECTION_NO;
   }
}


////////////////////////////////////////////////////////////////////
// Function to read pin voltage
////////////////////////////////////////////////////////////////////
float getPinVoltage(int pin) {
  //This function can be used for many different tasks in the labs
  //Study this line of code to understand what is going on!!
  //What does analogRead(pin) do?
  //Why is (float) needed?
  //Why divide by 1024?
  //Why multiply by 5?
  return 5 * (float)analogRead(pin) / 1024;
}

////////////////////////////////////////////////////////////////////
// Function to determine if a button is pushed or not
////////////////////////////////////////////////////////////////////
bool isButtonPushed(int button_pin) {
  //This function can be used to determine if a said button is pushed.
  //Remember that when the voltage is 0, it's only close to zero.
  //Hint: Call the getPinVoltage function and if that value is greater
  // than the BUTTON_THRESHOLD variable toward the top of the file, return true.
  if (getPinVoltage(button_pin) > BUTTON_THRESHOLD){
    return true;
  } else {
    return false;
  }
}


////////////////////////////////////////////////////////////////////
// Function that detects if there is an obstacle in front of robot
////////////////////////////////////////////////////////////////////
bool isCollision() {
  //This is where you add code that tests if the collision button 
  // was pushed (BUTTON_3)
  //In lab 6 you will add a sonar sensor to detect collision and
  // the code for the sonar sensor will go in this function.
  // Until then we will use a button to model the sensor.
  if (isButtonPushed(BUTTON_3)) {
    return true;
  } else {
    return false;
  }
}

////////////////////////////////////////////////////////////////////
// Function that detects if the capacitive sensor is being touched
////////////////////////////////////////////////////////////////////
bool isCapacitiveSensorTouched() {
  static CapacitiveSensor sensor = CapacitiveSensor(CAP_SENSOR_SEND, CAP_SENSOR_RECEIVE);
  long tau = sensor.capacitiveSensor(CAP_SENSOR_SAMPLES); 
  if (tau > CAP_SENSOR_TAU_THRESHOLD) {
    return true;
  } else {
    return false;
  }
}

////////////////////////////////////////////////////////////////////
// Function that detects if light is present
////////////////////////////////////////////////////////////////////
bool isLight(int pin) {
  float light = getPinVoltage(pin);
  Serial.print("light: ");
  Serial.println(light); // Use this line to test
  return (light > PHOTODIODE_LIGHT_THRESHOLD);
}


/**********************************************************************************************************
  Robot PLANNING - using the sensing to make decisions
 **********************************************************************************************************/
void RobotPlanning(void) {
  // The planning FSMs that are used by the robot to assign actions
  // based on the sensing from the Perception stage.
  fsmCollisionDetection(); // Milestone 1
  fsmMoveServoUpAndDown(); // Milestone 3
  // Add Speed Control State Machine in lab 4
  fsmCapacitiveSensorSpeedControl();
}

////////////////////////////////////////////////////////////////////
// State machine for detecting collisions, and stopping the robot
// if necessary.
////////////////////////////////////////////////////////////////////
void fsmCollisionDetection() {
  static int collisionDetectionState = 0;
  //Serial.print(collisionDetectionState); Serial.print("\t"); //uncomment for debugging
  
  switch (collisionDetectionState) {
    case 0: //collision detected
      //There is an obstacle, stop the robot
      ActionCollision = COLLISION_ON; // Sets the action to turn on the collision LED

      // stops robot from moving if collision is detected
      ActionRobotDrive = DRIVE_STOP;      
      
      //State transition logic
      if ( SensedCollision == DETECTION_NO) {
        collisionDetectionState = 1; //if no collision, go to no collision state
      }
      break;    
    
    case 1: //no collision
      //There is no obstacle, drive the robot
      ActionCollision = COLLISION_OFF; // Sets action to turn off the collision LED

      fsmSteerRobot(); // Milestone 2
      
      //State transition logic
      if (SensedCollision == DETECTION_YES) {
        collisionDetectionState = 0; //if collision, go to collision state
      }
      break;

    default: // error handling
      {
        collisionDetectionState = 0;
      }
      break;
  }
}

////////////////////////////////////////////////////////////////////
// State machine for detecting if light is to the right or left,
// and steering the robot accordingly.
////////////////////////////////////////////////////////////////////
void fsmSteerRobot() {
  static int steerRobotState = 0;
  //Serial.print(steerRobotState); Serial.print("\t"); //uncomment for debugging

  
  switch (steerRobotState) {
    case 0: //light is not detected
      ActionRobotDrive = DRIVE_STOP;
      
      //State transition logic
      if (SensedLightLeft == DETECTION_YES ) {
        steerRobotState = 1; //if light on left of robot, go to left state
      } else if (SensedLightRight == DETECTION_YES) {
        steerRobotState = 2; //if light on right of robot, go to right state
      }
      break;
    
    case 1: //light is to the left of robot
      //The light is on the left, turn left
      ActionRobotDrive =  DRIVE_LEFT; /*added*/
      
      //State transition logic
      if (SensedLightRight == DETECTION_YES) {
        steerRobotState = 3;
      } else if (SensedLightLeft == DETECTION_NO) {
        steerRobotState = 0;
      }
      break;
    
    case 2: //light is to the right of robot
      //The light is on the right, turn right
      ActionRobotDrive =  DRIVE_RIGHT;
      
      //State transition logic
      if (SensedLightLeft == DETECTION_YES) {
        steerRobotState = 3;
      } else if (SensedLightRight == DETECTION_NO) {
        steerRobotState = 0;
      }
      break;

    case 3://light is in front of robot
      //The light is straight, go straight
      ActionRobotDrive =  DRIVE_STRAIGHT;

      //State transition logic
      if (SensedLightLeft == DETECTION_NO) {
           steerRobotState = 2;
      } else if (SensedLightRight == DETECTION_NO) {
           steerRobotState = 1;
      }
      break;
            
    default: // error handling
    {
      steerRobotState = 0;
    }
  }
}

////////////////////////////////////////////////////////////////////
// State machine for detecting if light is above or below center,
// and moving the servo accordingly.
////////////////////////////////////////////////////////////////////
void fsmMoveServoUpAndDown() {
  static int moveServoState = 0;
  //Serial.print(moveServoState); Serial.print("\t"); //uncomment for debugging

  switch(moveServoState){
    case 0:
      ActionServoMove = SERVO_MOVE_STOP;

      if (SensedLightDown == DETECTION_YES){
        moveServoState = 1;
      } else if (SensedLightUp == DETECTION_YES){
        moveServoState = 2;
      }
      break;

    case 1: // moves the servo down
      ActionServoMove = SERVO_MOVE_DOWN;
      if (SensedLightUp == DETECTION_YES){
        moveServoState = 3;
      } else if (SensedLightDown == DETECTION_NO){
        moveServoState = 0;
      } 
      break;

    case 2: // moves the servo up
      ActionServoMove = SERVO_MOVE_UP;
      if (SensedLightDown == DETECTION_YES){
          moveServoState = 3;
      } else if (SensedLightUp == DETECTION_NO){
        moveServoState = 0;
      }
      break;
    
    case 3:
     ActionServoMove = SERVO_MOVE_STOP;
     if (SensedLightDown == DETECTION_NO){
       moveServoState = 2;
     } else if (SensedLightUp == DETECTION_NO){
       moveServoState = 1;
     }
     break;

    default: //error handling
      moveServoState = 0;
  }
  
}

////////////////////////////////////////////////////////////////////
// State machine for detecting when the capacitive sensor is
// touched, and changing the robot's speed.
////////////////////////////////////////////////////////////////////
void fsmCapacitiveSensorSpeedControl() {
  static int speedControlState = 0;

  switch(speedControlState){
    case 0: // checks if capacitor is touched
      if (SensedCapacitiveTouch == DETECTION_YES){
        speedControlState = 1;
      }
      break;

    case 1: // waits for capacitor release
      if (SensedCapacitiveTouch == DETECTION_NO){
        speedControlState = 2;
      }
      break;

    case 2: // changes the speed of the robot
      fsmChangeSpeed();
      speedControlState = 0;
      break;

    default:
      speedControlState = 0;
  }
}

////////////////////////////////////////////////////////////////////
// State machine for cycling through the robot's speeds.
////////////////////////////////////////////////////////////////////
void fsmChangeSpeed() {
   static int speedChangeState = 0;

  switch(speedChangeState){
    case 0:
      ActionRobotSpeed = SPEED_0;
      speedChangeState = 3;
      break;

    case 1:
      ActionRobotSpeed = SPEED_1;
      speedChangeState = 0;
      break;

    case 2: 
      ActionRobotSpeed = SPEED_2;
       speedChangeState = 1;
      break;

    case 3:
      ActionRobotSpeed = SPEED_3;
      speedChangeState = 2;
      break;

    default:
      speedChangeState = 0;
  }
  
}


/**********************************************************************************************************
  Robot ACTION - implementing the decisions from planning to specific actions
 ********************************************************************/
void RobotAction() {
  // Here the results of planning are implented so the robot does something

  // This turns the collision LED on and off
  switch(ActionCollision) {
    case COLLISION_OFF:
      doTurnLedOff(LED_3);
      break;
    case COLLISION_ON:
      doTurnLedOn(LED_3); 
      //Serial.print(getPinVoltage(BUTTON_TEMP));
      break;
  }
  
  // This drives the main motors on the robot
  switch(ActionRobotDrive) {
    case DRIVE_STOP:
      analogWrite(H_BRIDGE_ENA, 0);
      analogWrite(H_BRIDGE_ENB, 0);
      break;
    case DRIVE_LEFT:
      analogWrite(H_BRIDGE_ENA, ActionRobotSpeed);
      analogWrite(H_BRIDGE_ENB, 0);
      break;
    case DRIVE_RIGHT:
      analogWrite(H_BRIDGE_ENA, 0);
      analogWrite(H_BRIDGE_ENB, ActionRobotSpeed);
      break;
    case DRIVE_STRAIGHT:
      analogWrite(H_BRIDGE_ENA, ActionRobotSpeed);
      analogWrite(H_BRIDGE_ENB, ActionRobotSpeed);
      break;
  }
  
  // This calls a function to move the servo
    MoveServo();       
}


////////////////////////////////////////////////////////////////////
// Function that causes the servo to move up or down.
////////////////////////////////////////////////////////////////////
void MoveServo() {
  // Note that there needs to be some logic in the action of moving
  // the servo so that it does not exceed its range
  /* Add CurrentServoAngle in lab 6 */
  switch(ActionServoMove) {
    case SERVO_MOVE_STOP:
      doTurnLedOff(LED_1);
      doTurnLedOff(LED_5);
      break;
    case SERVO_MOVE_UP:
      doTurnLedOn(LED_5);
      break;
    case SERVO_MOVE_DOWN:
      doTurnLedOn(LED_1);
      break;
  }
}



/**********************************************************************************************************
  AUXILIARY functions that may be useful in performing diagnostics
 ********************************************************************/
// Function to turn LED on
void doTurnLedOn(int led_pin)
{
  digitalWrite(led_pin, HIGH);
}

// Function to turn LED off
void doTurnLedOff(int led_pin){
  digitalWrite(led_pin, LOW);
}
